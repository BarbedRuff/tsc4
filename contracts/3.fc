{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...
-}
global tuple builders;
forall X -> int is_null (X x) asm "ISNULL";

(int) getln(int x) {
  if(x == 0) {
    return 1;
  }
  int lenght = 0;
  while (x > 0){
    lenght += 1;
    x /= 2;
  }
  return lenght;
}

(int) pw(int n, int p) {
  int res = 1;
  while(p > 0){
    res *= n;
    p -= 1;
  }
  return res;
}

(tuple) int2tuple(int x) {
  tuple res = null();
  while (x > 0){
    res = cons(x % 2, res);
    x /= 2;
  }
  return res;
}

(builder) store(int x, builder build) {
  if ((x == 0) | (x == 1)) {
    if(builder_bits(build) == 1020){
      builders = cons(build, builders);
      build = begin_cell();
    }
    build~store_uint(x, 1);
  }
  else {
    tuple storedTuple = int2tuple(x);
    while(~ storedTuple.is_null()) {
      if(builder_bits(build) == 1020){
        builders = cons(build, builders);
        build = begin_cell();
      }
      build~store_uint(storedTuple~list_next(), 1);
    }
  }
  return build;
}

() recv_internal() {
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  int flag_len = getln(flag);
  builder build = begin_cell();
  builders = null();
  int bufer = 0;
  tuple stack = null();
  stack = cons(linked_list, stack);
  while (~ stack.is_null()) {
    slice s = stack~list_next().begin_parse();
    while (~ slice_data_empty?(s)){
      int bit = s~load_uint(1);
      if ((bufer == 0) & (bit == 0)) {
        build = store(bit, build);
      }
      else {
        bufer = bufer << 1;
        bufer += bit;
      }
      if ((flag_len == getln(bufer)) & (bufer != 0)) {
        if (bufer == flag) {
          build = store(value, build);
          bufer = 0;
        }
        else {
          build = store((bufer >> (flag_len - 1)), build);
          bufer = bufer - pw(2, (flag_len - 1));
          repeat (flag_len - getln(bufer) - 1) {
            build = store(0, build);
          }
        }
      }
    }
    if (s.slice_refs()) {
      stack = cons(s~load_ref(), stack);
    }
  }
  if (bufer != 0) {
    build = store(bufer, build);
  }
  if ((builders.is_null()) | (builder_bits(build) != 0)) {
    builders = cons(build, builders);
  }
  builder ans = builders~list_next();
  while (~ builders.is_null()) {
    builder last = builders~list_next();
    last = last.store_ref(ans.end_cell());
    ans = last;
  }
  return ans.end_cell();
}